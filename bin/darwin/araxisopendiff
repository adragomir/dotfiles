#!/usr/bin/osascript
# This script is required to reorder the parameters, so that they may be passed into the Merge Applescript API
on run args

  # make sure that the correct number of arguments are passed in
  if (count of args) is not 2 and (count of args) is not 4 and (count of args) is not 6 then
    return (count of args as text) & " arguments given!" & Usage()
  end if

  tell application "Araxis Merge"
    set _file1 to item 1 of args as text
    set _file2 to item 2 of args as text
    set _ancestorFile to ""
    set _mergeFile to ""

    if count of args >= 4 then
      if item 3 of args is equal to "-ancestor"
        set _ancestorFile to item 4 of args as text
      else
        if item 3 of args is equal to "-merge"
          set _mergeFile to item 4 of args as text
        else
          return "Third argument must be '-merge' or '-ancestor'!"
        end if
      end if
    end if 

    if count of args is 6 then
      if item 5 of args is equal to "-ancestor"
        if item 3 of args is equal to "-ancestor"
          return "There must be only one '-ancestor'! "
        end if
        set _ancestorFile to item 6 of args as text
      else
        if item 5 of args is equal to "-merge"
          if item 3 of args is equal to "-merge"
            return "There must be only one '-merge'! "
          end if
          set _mergeFile to item 6 of args as text
        else
          return "Fifth argument must be '-merge' or '-ancestor'!"
        end if
      end if
    end if 


    if not _file1 starts with "/"
      set _file1 to (POSIX path of (POSIX file "." as alias)) & _file1
    end if
    if not _file2 starts with "/"
      set _file2 to (POSIX path of (POSIX file "." as alias)) & _file2
    end if
    if _ancestorFile is not equal to "" and not _ancestorFile starts with "/"
      set _ancestorFile to (POSIX path of (POSIX file "." as alias)) & _ancestorFile
    end if
    if _mergeFile is not equal to "" and not _mergeFile starts with "/"
      set _mergeFile to (POSIX path of (POSIX file "." as alias)) & _mergeFile
    end if

    if _ancestorFile is equal to "" then
      set _document to compare {_file1, _file2}
    else
      set _document to compare {_file1, _ancestorFile, _file2}
    end if

    tell _document
      if _mergeFile is not equal to ""
        set the (first compared file's save path) to _mergeFile
        set the (second compared file's save path) to _mergeFile
        if _ancestorFile is not equal to ""
          set the (third compared file's save path) to _mergeFile
        end if
      end if   
      activate
    end tell
  end tell
end run

on Usage()
	return "
Usage is: araxisopendiff <file1> <file2> [-ancestor <ancestorFile>] [-merge <mergeFile>] "
end Usage
